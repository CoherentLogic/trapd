TRAPD
 Q
 ;
INETD
 ;
 D SSET("RMT_IP",$ZTRNLNM("TCPREMOTEIP"))
 D SSET("RMT_HOST",$ZTRNLNM("TCPREMOTEHOST"))
 D SSET("RMT_PORT",$ZTRNLNM("TCPREMOTEPORT"))
 D SSET("LOCAL_IP",$ZTRNLNM("TCPLOCALIP"))
 D SSET("LOCAL_HOST",$ZTRNLNM("TCPLOCALHOST"))
 D SSET("LOCAL_PORT",$ZTRNLNM("TCPLOCALPORT"))
 D SSET("SEQ",100)
 S MESG="TRAPD/1.0 ["_$$SGET("LOCAL_HOST")_" "_$$SGET("LOCAL_PORT")_"]"
 D MESG("---",MESG)
 D MESG(100,"CONNECTION OK FROM "_$$SGET("RMT_HOST")_" ["_$$SGET("RMT_IP")_":"_$$SGET("RMT_PORT")_"]")
 D AUTHINIT
 D REPL
 D QUITTRAP
 Q
 ;
AUTHINIT
 D SSET("AUTH",0)
 D SSET("ANAM","")
 D MESG("200","AWAITING AUTHENTICATION RECORD")
 Q
 ;
SSET(KEY,VALUE)
 S ^TRAPD($J,"MTA",KEY)=VALUE
 Q
 ;
SGET(KEY)
 Q $G(^TRAPD($J,"MTA",KEY))
 ;
REPL
 N ISTR,CMD,SEQ,PARM S (ISTR,CMD,PARM)=""
 N CMDOK S CMDOK=0
 N MALF S MALF=0
 F  D
 . I $$SGET("AUTH")=1 D
 . . N PID,MSG,TRG S PID="",TRG=$$SGET("ANAM")
 . . S MSG=""
 . . F  S MSG=$O(^TRAPSESS(TRG,$J,"MQUEUE",MSG)) Q:MSG=""  D
 . . . D MESG("500",^TRAPSESS(TRG,$J,"MQUEUE",MSG)_" "_MSG)
 . . . K ^TRAPSESS(TRG,$J,"MQUEUE",MSG)
 . R ISTR
 . S CMDOK=0,MALF=0
 . I $L(ISTR," ")<3 D MESG(302,"MALFORMED INSTRUCTION SEQUENCE") S MALF=1
 . I MALF=0 D
 . . S SEQ=$P(ISTR," ",1)
 . . S CMD=$P(ISTR," ",2)
 . . S CMD=$TR(CMD,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 . . S PARM=$P(ISTR," ",3,$L(ISTR," "))
 . . D SSET("SEQ",SEQ)
 . . I CMD="PING" S CMDOK=1 D PONG
 . . I CMD="STAT" S CMDOK=1 D STAT(PARM)
 . . I CMD="AUTH" S CMDOK=1 D AUTH(PARM)
 . . I CMD="SGNF" S CMDOK=1 D QUITTRAP
 . . I CMD="MESG" S CMDOK=1 D ENQUEUE(PARM)
 . . I CMD="ADDF" S CMDOK=1,^TRAPFL($$SGET("ANAM"),$P(PARM," ",1))=""
 . . I CMD="DELF" S CMDOK=1 K ^TRAPFL($$SGET("ANAM"),$P(PARM," ",1))
 . . I CMD="LIST" S CMDOK=1 D LIST
 . . I CMD="NOP" S CMDOK=1
 . . I CMDOK=0 D MESG(301,"INSTRUCTION INVALID")
 . . I CMDOK=1 D MESG(300,"INSTRUCTION OK")
 Q
 ;
LIST
 I $$SGET("AUTH")=1 D
 . N TRG S TRG=$$SGET("ANAM")
 . N FRIEND S FRIEND=""
 . F  S FRIEND=$O(^TRAPFL(TRG,FRIEND)) Q:FRIEND=""  D
 . . I $D(^TRAPSESS(FRIEND)) D
 . . . D MESG("403",FRIEND)
 . . E  D
 . . . D MESG("404",FRIEND)
 E  D
 . D MESG(203,"INSTRUCTION INVALID IN CURRENT PRIVILEGE LEVEL")
 Q
 ;
PONG
 D MESG("102","PING RECEIVED")
 Q
 ;
STAT(PARM)
 I $$SGET("AUTH")=1 D
 . N UID S UID=$P(PARM," ",1)
 . I $D(^TRAPSESS(UID)) D 
 . . D MESG("403",UID)
 . E  D
 . . D MESG("404",UID)
 E  D
 . D MESG(203,"INSTRUCTION INVALID IN CURRENT PRIVILEGE LEVEL")
 Q
 ;
ENQUEUE(PARM)
 I $$SGET("AUTH")=1 D
 . N TRG,MSG
 . S TRG=$P(PARM," ",1),MSG=$P(PARM," ",2,$L(PARM," "))
 . N PID S PID=""
 . F  S PID=$O(^TRAPSESS(TRG,PID)) Q:PID=""  D
 . . S ^TRAPSESS(TRG,PID,"MQUEUE",MSG)=$$SGET("ANAM")
 E  D
 . D MESG(203,"INSTRUCTION INVALID IN CURRENT PRIVILEGE LEVEL")
 Q
 ;
MESG(CODE,MSG)
 N CRLF S CRLF=$C(13)_$C(10)
 N SEQ S SEQ=$$SGET("SEQ")
 W SEQ," ",CODE," ",MSG,CRLF
 D SSET("SEQ",SEQ+1)
 Q
 ;
AUTH(PARM)
 N SUCCESS S SUCCESS=0
 N UID,PW S (UID,PW)=""
 S UID=$P(PARM," ",1)
 S PW=$P(PARM," ",2)
 I $D(^TRAPAUTH(UID)) D
 . I ^TRAPAUTH(UID)=PW S SUCCESS=1
 I SUCCESS=1 D
 . D SSET("AUTH",1)
 . D SSET("ANAM",UID)
 . D SSET("AMOD",^TRAPAUTH(UID,"AMOD"))
 . S ^TRAPSESS(UID,$J)=$$SGET("RMT_IP")_":"_$$SGET("RMT_PORT")
 . D MESG(201,"CREDENTIALS ACCEPTED, PRIV LEVEL ["_$$SGET("AMOD")_"]")
 E  D
 . D SSET("AUTH",0)
 . D SSET("ANAM","")
 . D SSET("AMOD","")
 . D MESG(202,"CREDENTIALS DENIED")
 Q
 ;
QUITTRAP
 I $$SGET("AUTH")=1 K ^TRAPSESS($$SGET("ANAM"),$J)
 D MESG("199","GOODBYE")
 K ^TRAPD($J)
 HALT
 Q